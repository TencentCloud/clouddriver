plugins {
  id 'com.google.protobuf' version '0.8.10'
}

tasks.compileGroovy.enabled = false
sourceSets.main.java.srcDirs = ['src/main/java']

def grpcVersion = '1.25.0'

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.10.0"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}

dependencies {
  implementation project(":clouddriver-core")
  implementation project(":cats:cats-core")
  implementation project(":clouddriver-security")
  implementation "io.grpc:grpc-protobuf:${grpcVersion}"
  implementation "io.grpc:grpc-stub:${grpcVersion}"
  implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"

  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"
  testAnnotationProcessor "org.projectlombok:lombok"

  implementation "org.codehaus.groovy:groovy-all"

  implementation "com.google.code.findbugs:jsr305"
  implementation "com.google.guava:guava"
  implementation "com.netflix.spectator:spectator-api"
  implementation "com.netflix.spinnaker.fiat:fiat-core:$fiatVersion"
  implementation "com.netflix.spinnaker.kork:kork-config"
  implementation "org.apache.commons:commons-lang3"
  implementation "org.springframework.boot:spring-boot-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"

  testImplementation "cglib:cglib-nodep"
  testImplementation "org.objenesis:objenesis"
  testImplementation "org.spockframework:spock-core"
  testImplementation "org.spockframework:spock-spring"
  testImplementation "org.springframework:spring-test"
}
